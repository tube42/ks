
buildscript {
  repositories {
        jcenter()
   }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

allprojects {
	repositories {
		jcenter()
	}

	apply plugin: 'java'
    apply plugin: 'java-library'
	apply plugin: 'maven'
	apply plugin: 'maven-publish'

    dependencies {
		testCompile 'junit:junit:4.12'
	}

    // custom tasks for creating source/javadoc jars
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    artifacts {
        archives sourcesJar, javadocJar
    }
}


version = '0.3.0'
group = 'se.tube42.ks'

publishing {
	publications {
		MyPublication(MavenPublication) {
			from components.java

			groupId project.group
            artifactId 'ks'
            version project.version

            // publish sources and javadocs too
            artifact sourcesJar
            artifact javadocJar
		}
	}
}


bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
	publications = ['MyPublication']
	pkg {
		repo = 'maven'
		name = 'ks'
        desc = 'ks library'
		userOrg = user
		licenses = ['LGPL-2.1']
        websiteUrl = 'https://github.com/tube42/ks'
        issueTrackerUrl = 'https://github.com/tube42/ks/issues'
		vcsUrl = 'https://github.com/tube42/ks.git'
		labels = ['game-library','FSM', 'scheduler']
        githubRepo = 'tube42/ks'
        githubReleaseNotesFile = 'README.rst'
        publicDownloadNumbers = true
		version {
			name = project.version
			desc = 'KS library ' + project.version
			vcsTag = project.version
		}
	}
}


jar {
    manifest {
        attributes('Provider': 'gradle',
            'Implementation-Title': project.name,
            'Implementation-Version': project.version)
    }
}

test {
    outputs.upToDateWhen{false}
    afterTest { desc, result ->
        logger.quiet "Testing ${desc.name} (${desc.className}): ${result.resultType}"
    }
}